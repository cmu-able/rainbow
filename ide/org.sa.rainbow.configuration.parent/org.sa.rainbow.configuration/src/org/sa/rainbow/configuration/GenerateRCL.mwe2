module org.sa.rainbow.configuration.GenerateConfigModel

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
//	bean = StandaloneSetup {
//		scanClassPath = true
//		platformUri= "${runtimeProject}/.."
//		registerGenModelFile = "platform:/resource/org.sa.rainbow.configuration/model/RCL.ecore"
//	}
	
		
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.sa.rainbow.configuration"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
			
		}
		language = StandardLanguage {
			name = "org.sa.rainbow.configuration.rcl"
			fileExtensions = "rbw"
			
			referencedResource="platform:/resource/org.sa.rainbow.stitch/model/generated/Stitch.genmodel"
			referencedResource="platform:/resource/org.acme.xtext/model/generated/Acme.genmodel"
			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			junitSupport = {
				junitVersion = "5"
			}
			parserGenerator = {
				options  = {
					backtrackLexer = false
				}
			
			}
			projectWizard = {
				generate = true
				pluginProject = false
				
			}
			
			fileWizard = {
				generate = true
			}
			
			fragment = ui.codemining.CodeMiningFragment {
				generateStub = true
				generateXtendStub = false
			}
			

		}
	}
}
